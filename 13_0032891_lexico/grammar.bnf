# Based on https://cs.wmich.edu/~gupta/teaching/cs4850/sumII06/The%20syntax%20of%20C%20in%20Backus-Naur%20form.htm

<translation-unit> ::= {<external-declaration>}*

<external-declaration> ::= <function-definition>
                         | <declaration>

<function-definition> ::= <type-specifier> <declarator> <compound-statement>

<type-specifier> ::= int
                   | float
                   | elem
                   | set

<declarator> ::= <identifier> ( {<parameter-list>}* )

<parameter-list> ::= <parameter-declaration>
                   | <parameter-list> , <parameter-list>

<parameter-declaration> ::= <type-specifier> <identifier>

<conditional-expression> ::= <logical-or-expression>

<logical-or-expression> ::= <logical-and-expression>
                          | <logical-or-expression> || <logical-and-expression>

<logical-and-expression> ::= <equality-expression>
                           | <logical-and-expression> && <equality-expression>

<equality-expression> ::= <relational-expression>
                        | <equality-expression> == <relational-expression>
                        | <equality-expression> != <relational-expression>

<relational-expression> ::= <additive-expression>
                          | <relational-expression> < <additive-expression>
                          | <relational-expression> > <additive-expression>
                          | <relational-expression> <= <additive-expression>
                          | <relational-expression> >= <additive-expression>

<additive-expression> ::= <multiplicative-expression>
                        | <additive-expression> + <multiplicative-expression>
                        | <additive-expression> - <multiplicative-expression>

<multiplicative-expression> ::= <unary-expression>
                              | <multiplicative-expression> * <unary-expression>
                              | <multiplicative-expression> / <unary-expression>

<unary-expression> ::= <postfix-expression>
                     | <unary-operator> <unary-expression>

<postfix-expression> ::= <primary-expression>
                       | <postfix-expression> ( {<assignment-expression>}* )

<primary-expression> ::= <identifier>
                       | <constant>
                       | ( <expression> )

<constant> ::= <integer-constant>
             | <character-constant>
             | <floating-constant>
             | <empty-constant>
             | <string>

<expression> ::= <assignment-expression>
               | <type-check-expression>
               | <expression> , <assignment-expression>

<assignment-expression> ::= <conditional-expression>
                          | <unary-expression> = <assignment-expression>

<unary-operator> ::= +
                   | -
                   | !

<compound-statement> ::= { {<declaration>}* {<statement>}* }

<declaration> ::= <type-qualifier> <identifier> ;

<statement> ::= <expression-statement>
              | <compound-statement>
              | <selection-statement>
              | <iteration-statement>
              | <inclusion-statement> ;
              | <removal-statement>
              | <io-statement>
              | <jump-statement>

<expression-statement> ::= {<expression>}? ;

<membership-expression> ::= <expression> in <expression>

<type-check-expression> ::= is_set ( <identifier> )

<selection-statement> ::= if ( <expression> ) <statement>
                        | if ( <expression> ) <statement> else <statement>
                        | <existence-statement>

<existence-statement> ::= exists ( <membership-expression> )
                        | exists ( <identifier> in <identifier> )

<iteration-statement> ::= for ( {<expression>}? ; {<expression>}? ; {<expression>}? ) <statement>
                        | forall ( <membership-expression> ) <statement>

<inclusion-statement> ::= <inclusion-expression> ;

<inclusion-expression> ::= add ( <membership-expression> )
                        | add ( <expression> in <inclusion-statement> )

<removal-statement> ::= remove ( <membership-expression> ) ;

<io-statement> ::= write ( <expression> ) ;
                | writeln ( <expression> ) ;
                | read ( <identifier> ) ;

<jump-statement> ::= return {<expression>}? ;
